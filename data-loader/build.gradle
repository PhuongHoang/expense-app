ext {
    set('mapstructVersion', '1.3.0.Final')
    set('springCloudVersion', "Greenwich.SR1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.cloud:spring-cloud-starter-task'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // Have to use compile to allow IntelliJ to pick up the generated annotations
    compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation project(':data')
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

sourceSets.configureEach { sourceSet ->
    tasks.named(sourceSet.compileJavaTaskName).configure {
        options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated/sources/annotationProcessor/java/${sourceSet.name}")
    }
}

compileJava {
    options.compilerArgs = [
            // See http://mapstruct.org/documentation/dev/reference/html/#configuration-options
            '-Amapstruct.defaultComponentModel=spring'
    ]
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}