plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE' apply false
}

subprojects {
    version = '1.0'
}

configure(subprojects.findAll { it.name != 'expense-ui' }) {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'
    group = 'com.example'
    version = '1.0'

    ext {
        junitVersion = '5.3.1'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit' //by both name and group
        }
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configure(subprojects.findAll { it.name == 'data-loader' || it.name == 'expense-tracker' }) {

    apply plugin: 'org.springframework.boot'

    dependencies {
        runtimeOnly 'org.postgresql:postgresql'
    }
}